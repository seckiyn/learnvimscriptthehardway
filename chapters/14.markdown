Otokomut Grupları
==================

Birkaç bölüm önce otokomutları öğrendik. Şu komutu çalıştırın:

    :::vim
    :autocmd BufWrite * :echom "Writing buffer!"

Şimdi üzerinde bulunduğunuz bufferı `:write` komutu ile kaydedin ve mesaj
kayıtlarını görmek için `:messages` komutunu çalıştırın. Mesaj listesinde
`Writing buffer!` yazısını görmeniz lazım.

Şimdi bufferı tekrar kaydedin ve mesaj listesini görmek için `:messages`
komutunu çalıştırın. Bu sefer listede iki defa `Writing buffer!` yazısını
göreceksiniz.

Şimdi aynı otokomutu çalıştırın:

    :::vim
    :autocmd BufWrite * :echom "Writing buffer!"

Şu anki bufferı bir kez daha kaydedin ve `:messages` komutunu çalıştırın.
Listede `Writing buffer!` yazısını *dört* tane göreceksiniz. Ne oldu?

Böyle otokomut yarattığınızda Vim'in önceki otokomutu silip silmemesi gerektiği
hakkında herhangi bir bilgisi olamaz. Bu durumda Vim aynı şeyi yapan iki
*farklı* otokomut yarattı.

Sorun
-----

Şimdi otokomutların kopyalarını yaratabilmenin mümkün olduğunu biliyorsunuz,
şöyle düşünüyor olabilirsiniz: "Ne olacak? Böyle bir şey yapma gitsin!"

`~/.vimrc` dosyasını kaynaklamanın sorunu tüm dosyayı yeniden okumasıdır,
otokomutlar da dahil olmak üzere! Bu demek oluyor ki ne zaman `~/.vimrc`
dosyanızı kaynaklasanız bir sürü otokomutu kopyalamış olacaksınız ki bu da aynı
komutu sürekli çalıştıracağı için Vim'in yavaş çalışmasına neden olacak.

Bunun bir örneğini görmek için şu komutu çalıştırın:

    :::vim
    :autocmd BufWrite * :sleep 200m

Şimdi şu anki bufferı kaydedin. Fark etmemiş olabilirsiniz ancak Vim'in dosyayı
yazması sırasında küçük bir yavaşlık oldu. Şimdi aynı komutu üç kez daha
çalıştırın:

    :::vim
    :autocmd BufWrite * :sleep 200m
    :autocmd BufWrite * :sleep 200m
    :autocmd BufWrite * :sleep 200m

Dosyayı tekrar kaydedin. Bu sefer yavaşlık çok daha belirgin olacak.

Tabii ki uyumaktan başka bir şey yapmayan komutlarınız olamayacak ama
`~/.vimrc` dosyanızı modifiye ettikçe kolayca 1,000 satıra ulaşabilirsiniz ki
bunların bazıları da otokomutlar olacak. Bunları pluginlerle eklenmiş
olabilecek otokomutları da ekleyince kesinlikle performansı etkileyecek.

Otokomutları Gruplamak
----------------------

Vim'in bu soruna bir çözümü var. İlk adım bağıntılı otokomutları
isimlendirilmiş gruplara koymak.

Daha önceden yazdığım otokomutlardan kurtulmak için sıfırdan bir Vim açın ve şu
komutları çalıştırın:

    :::vim
    :augroup testgroup
    :    autocmd BufWrite * :echom "Foo"
    :    autocmd BufWrite * :echom "Bar"
    :augroup END

Ortadaki iki satırın hizalaması önemsiz. Istemiyorsanız boşlukları yazmanıza
gerek yok.

Bir bufferı kaydedin ve `:messages` sayfasını kontrol edin. Hem `Foo` hem de
`Bar` yazılarını görmeniz lazım. Şimdi şu komutları çalıştırın.

    :::vim
    :augroup testgroup
    :    autocmd BufWrite * :echom "Baz"
    :augroup END

Şimdi bufferı tekrar kaydettiğimizde ne olacak tahmin etmeye çalışın. Aklınızda
bir fikir oluşunca bufferı kaydedin ve `:messages` sayfasını kontrol edin doğru
olup olmadığınızı görmek için.

Grupları Temizlemek
-------------------

Dosyayı yazdığınızda ne oldu? Beklediğiniz şey miydi?

Eğer ki Vim'in yeni grubu önceki grubun üzerine yazacağını tahmin ettiyseniz,
doğru tahmin edemediniz. Merak etmeyin, çoğu insan aynı şekilde düşünürdü (Ben
de dahil olmak üzere)

`augroup` komutunu birkaç kez kullandığınızda Vim onları *birleştirecek*.

Eğer ki bir grubu *temizlemek* istiyorsanız grubun içinde `autocmd!` komutunu
kullanmanız gerekiyor. Şu komutları çalıştırın:

    :::vim
    :augroup testgroup
    :    autocmd!
    :    autocmd BufWrite * :echom "Cats"
    :augroup END

Şimdi dosyanızı kaydedin ve `:messages` sayfasını kontrol edin. Dosyayı
kaydettiğinizde bu sefer Vim sadece `Cats` yazısını yazdı.

Vimrc Dosyanızda Otokomutları Kullanmak
---------------------------------------

Şimdi nasıl otokomut yaratılır ve temizlenir biliyoruz, şimdi bu otokomutları
`~/.vimrc` dosyamıza ekleyebiliriz ve her kaynakladığımızda kopyalarını
da yaratmayacak.

Şu komutları `~/.vimrc` dosyanıza ekleyin:

    :::vim
    augroup filetype_html
        autocmd!
        autocmd FileType html nnoremap <buffer> <localleader>f Vatzf
    augroup END

`filetype_html` grubuna girdiğimiz gibi onu temizliyoruz, daha sonra bir
otokomut tanımlıyoruz ve grubu terk ediyoruz. Temizlemek eğer ki `~/.vimrc`
dosyamızı tekrar kaynaklarsak otokomut kopyalarının yaratılmasını önleyecek.

Egzersizler
-----------

`~/.vimrc` dosyanızı gezin ve *her* otokomutu yaptığımız gibi grup içine sarın.
Size mantıklı gelen otokomutları aynı gruba ekleyebilirsiniz.

Son örnekteki eşlemenin ne yaptığını anlamaya çalışın.

`:help autocmd-groups` sayfasını okuyun.

